$PBExportHeader$ftc_imagen_articulo_ruta.srf
global type ftc_imagen_articulo_ruta from function_object
end type

forward prototypes
global function string ftc_imagen_articulo_ruta (string tipo, string articulo)
end prototypes

global function string ftc_imagen_articulo_ruta (string tipo, string articulo);// tipo: Codigo de los diferentes tipos de imágen de la tabla "archivo_resolucion"
// coleccion: de la que deseamos cargar la imágen
// modelo: del que deseamos cargar la imágen
// pieza: del que deseamos cargar la imágen
// Cargaremos la imágen en función de los parámetros que no estén vacios (coleccion, modelo, pieza)

String resultado = ""
string codigo_archivo, ruta, ruta_archivos_exposicion, tamanyo, coleccion, modelo, pieza, ruta_vacia
Long min_codigo_archivo

if tipo <> "0" then
	SELECT tamanyo INTO :tamanyo FROM archivo_resolucion WHERE empresa = :codigo_empresa AND codigo = :tipo;
	tamanyo = tamanyo + '\'
else
	tamanyo = ""
end if

SELECT ruta_archivos_exposicion INTO :ruta_archivos_exposicion  FROM empresas WHERE empresa = :codigo_empresa;
if isnull(ruta_archivos_exposicion) then ruta_archivos_exposicion = ""

SELECT REPLACE(:ruta_archivos_exposicion + ruta + :tamanyo + fichero, ' ', ''), fichero INTO :ruta, :ruta_vacia FROM articulos_imagen_encajado WHERE empresa = :codigo_empresa AND articulo = :articulo AND produccion = 1 and fichero is not null and fichero <> '';

if not isnull(ruta_vacia) AND ruta_vacia <> "" then
	//Ruta encontrada. Terminamos
	return ruta
end if

//buscamos alternativas para no dejar en blanco la imagen
SELECT modelo.coleccion, articulos.modelo_origen, articulos.pieza_origen INTO :coleccion, :modelo, :pieza 
FROM articulos 
INNER JOIN modelo ON articulos.empresa = modelo.empresa AND articulos.modelo_origen = modelo.modelo 
WHERE articulos.empresa = :codigo_empresa AND articulos.codigo = :articulo;

if not isnull(pieza) and pieza <> "" then
	// Imágen de la pieza	
	
	SELECT MIN(CONVERT(integer,codigo)) 
	INTO :min_codigo_archivo
	FROM modelo_pieza_archivo 
	WHERE empresa = :codigo_empresa
	and pieza = :pieza;
	
	if not isnull(min_codigo_archivo) and min_codigo_archivo > 0 then
		codigo_archivo = String(min_codigo_archivo)
		SELECT REPLACE(ruta + :tamanyo + fichero, ' ', '' ) 
		INTO  :ruta  
		FROM modelo_pieza_archivo
		WHERE empresa = :codigo_empresa
		and pieza = :pieza
		and codigo = :codigo_archivo 
		and fichero <> '' and fichero IS NOT NULL 
		USING SQLCA;
		
		if not isnull(ruta) and ruta <> "" then
			resultado =  ruta_archivos_exposicion + ruta
		end if
	end if
	
else 
	if not isnull(coleccion) and coleccion <> "" and not isnull(modelo) and modelo <> "" then
		
		SELECT TOP 1 REPLACE(ruta + :tamanyo + fichero, ' ', '' ) 
		INTO  :ruta  
		FROM modelo_archivo_expo
		WHERE empresa = :codigo_empresa
		and coleccion = :coleccion
		and modelo = :modelo
		and produccion = 1 
		and fichero <> '' and fichero IS NOT NULL 
		USING SQLCA;
		
		if not isnull(ruta) and ruta <> "" then
			resultado = ruta_archivos_exposicion + ruta
		end if
			
	else 
		if not isnull(coleccion) and coleccion <> "" then
			// Imágen de la colección
			SELECT MIN(CONVERT(integer,codigo)) 
			INTO :min_codigo_archivo
			FROM coleccion_archivo 
			WHERE empresa = :codigo_empresa
			and coleccion = :coleccion;
			
			if not isnull(min_codigo_archivo) and min_codigo_archivo > 0 then
				codigo_archivo = String(min_codigo_archivo)
				SELECT REPLACE(ruta + :tamanyo + fichero, ' ', '' ) 
				INTO  :ruta  
				FROM coleccion_archivo
				WHERE empresa = :codigo_empresa
				and coleccion = :coleccion
				and codigo = :codigo_archivo 
				and fichero <> '' and fichero IS NOT NULL 
				USING SQLCA;
				
				if not isnull(ruta) and ruta <> "" then
					resultado =  ruta_archivos_exposicion + ruta
				end if
			end if
		end if
	end if
end if

if isnull(resultado) or resultado = "" then
	//BUSCAMOS IMAGEN ENCAJADO
	String encajados[]
	if ftc_articulo_base(articulo) then return ""
	f_busca_encajados_rec_una_rama(articulo, encajados)
	if UpperBound(encajados) = 1 then
		SELECT REPLACE(:ruta_archivos_exposicion + ruta + :tamanyo + fichero, ' ', ''), fichero INTO :ruta, :ruta_vacia FROM articulos_imagen_encajado WHERE empresa = :codigo_empresa AND articulo = :encajados[1] AND produccion = 1 and fichero is not null and fichero <> '';
		if not isnull(ruta_vacia) AND ruta_vacia <> "" then
			return ruta
		else
			return ""
		end if
	else
		return ""
	end if
else
	return resultado 
end if

end function

