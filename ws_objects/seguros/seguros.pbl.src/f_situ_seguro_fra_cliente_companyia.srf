$PBExportHeader$f_situ_seguro_fra_cliente_companyia.srf
$PBExportComments$Devuelve (No solicitado,Solicitado,Concedido,Primera venta)
global type f_situ_seguro_fra_cliente_companyia from function_object
end type

forward prototypes
global function string f_situ_seguro_fra_cliente_companyia (string arg_empresa, integer arg_anyo, long arg_fra, datetime arg_fecha_fra, string arg_cliente, string arg_companyia)
end prototypes

global function string f_situ_seguro_fra_cliente_companyia (string arg_empresa, integer arg_anyo, long arg_fra, datetime arg_fecha_fra, string arg_cliente, string arg_companyia);long     orden,orden_anterior,facturas_anteriores
int      mes,anyo
datetime fecha,fecha_anterior,final_mes
string   situacion,situacion_anterior

select max(fecha)
into   :fecha
from   venriesgoclienteshis
where  empresa   =  :arg_empresa
and    cliente   =  :arg_cliente
and    fecha    <= :arg_fecha_fra
and    companyia = :arg_companyia;

if isnull(fecha) then
	situacion = "No solicitado"
else
	select max(orden)
	into   :orden
	from   venriesgoclienteshis
	where  empresa   = :arg_empresa
	and    cliente   = :arg_cliente
	and    fecha     = :fecha
	and    companyia = :arg_companyia;
	
	select situacion
	into   :situacion
	from   venriesgoclienteshis
	where  empresa   = :arg_empresa
	and    cliente   = :arg_cliente
	and    fecha     = :fecha
	and    orden     = :orden
	and    companyia = :arg_companyia;
	
	if isnull(situacion) then situacion = ""
	
	if situacion = "Solicitado" then
		//Comprobamos si se solicita una revisión
		
		select max(fecha)
		into   :fecha_anterior
		from   venriesgoclienteshis
		where  empresa   = :arg_empresa
		and    cliente   = :arg_cliente
		and    fecha    <= :fecha
		and    orden    <> :orden
		and    companyia = :arg_companyia;
		
		if not isnull(fecha_anterior) then
			select max(orden)
			into   :orden_anterior
			from   venriesgoclienteshis
			where  empresa   = :arg_empresa
			and    cliente   = :arg_cliente
			and    fecha     = :fecha_anterior
			and    orden    <> :orden
			and    companyia = :arg_companyia;
			
			select situacion
			into   :situacion_anterior
			from   venriesgoclienteshis
			where  empresa   = :arg_empresa
			and    cliente   = :arg_cliente
			and    fecha     = :fecha_anterior
			and    orden     = :orden_anterior
			and    companyia = :arg_companyia;
			
			if isnull(situacion_anterior) then situacion_anterior = ""			
			
			if trim(situacion_anterior) <> "" then
				situacion = situacion_anterior
			end if
		end if

	end if
end if

if situacion = "No solicitado" then
	//Comprobamos si es la primera fra del cliente
	
	select count(*)
	into   :facturas_anteriores
	from   venfac
	where  empresa   = :arg_empresa
	and    cliente   = :arg_cliente
	and    ffactura <= :arg_fecha_fra
	and    (anyo <> :arg_anyo or factura <> :arg_fra);
	
	if isnull(facturas_anteriores) then facturas_anteriores = 0
	
	if facturas_anteriores = 0 then
		situacion = "Primera venta"
	end if
end if

//Si la situación es solicitado comprobamos la situación del cliente a final del mes
if situacion = "Solicitado" then
	mes  = month(date(arg_fecha_fra))
	anyo = year(date(arg_fecha_fra))
	if mes = 12 then
		mes = 1
		anyo ++
	else
		mes ++
	end if
	
	final_mes = datetime(relativedate(mdy(mes,1,anyo),-1))
	
	situacion = f_situacion_cliente_asegurado_fecha(arg_empresa,arg_companyia,arg_cliente,final_mes)
	//Si la situación a final del mes es denagado marcamo la factura como solicitada
	if situacion = "Denegado" then
		situacion = "Solicitado"
	end if
end if

return situacion
end function

